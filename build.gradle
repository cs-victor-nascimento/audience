buildscript {

    ext.versions = [
            targetSdk: 27,
            supportLibrary: '27.1.1',
            retrofit: '2.4.0',
            okHttp: '3.10.0',
            // Fixtures
            requestMatcher: '2.2.0',

            // Test local
            junit: '4.12',
            hamcrest: '1.3',
            robolectric: '3.8',

            // Test instrumented
            espresso: '3.0.1',
            supportRunner: '1.0.1'
    ]


    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.7.2'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == 'com.android.support'
                        && details.requested.name != 'multidex'
                        && details.requested.name != 'multidex-instrumentation') {
                    details.useVersion versions.supportLibrary
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

void includeTestDeps(dependencies) {

    // Local test deps
    dependencies.testImplementation "junit:junit:$versions.junit"
    dependencies.testImplementation "org.robolectric:robolectric:$versions.robolectric"
    dependencies.testImplementation "org.hamcrest:hamcrest-all:$versions.hamcrest"
    dependencies.testImplementation "br.com.concretesolutions:requestmatcher:$versions.requestMatcher"

    // Instrumented tests deps
    dependencies.androidTestImplementation "com.android.support:support-annotations:$versions.support"
    dependencies.androidTestImplementation "com.android.support.test.espresso:espresso-core:$versions.espresso"
    dependencies.androidTestImplementation "com.android.support.test:runner:$versions.supportRunner"
    dependencies.androidTestImplementation "br.com.concretesolutions:requestmatcher:$versions.requestMatcher"
    dependencies.androidTestImplementation "com.squareup.okhttp3:mockwebserver:$versions.okHttp"
}
